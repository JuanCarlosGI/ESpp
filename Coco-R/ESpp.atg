using System.Collections.Generic;
using System.Linq;

COMPILER ESpp

CHARACTERS
	digit = '0'..'9'.
	lower = 'a'..'z'.
	upper = 'A'..'Z'.
	eol = '\r' + '\n'.
	noQuote = ANY - '\"' - eol.

TOKENS
	id = lower{['_'](lower|upper|digit)}.
	ctestr = '"' {noQuote} '"'.
	cteent = digit{digit}.
	ctedbl = digit{digit}'.'digit{digit}.
	lpar = '('.

IGNORE eol + '\t'

PRODUCTIONS
	ESpp = Program.

	Program	= 
		{
			IF(DoubleFollowedByLPar()) DeclaracionFunc
			| Declaracion 
		}												(. CheckFunctionsNoBody(); .) 
		Main.

	DeclaracionFunc =											(. Type funType = Type.Error; .)
		(
			Tipo<out funType>
			|"rutina"											(. funType = Type.Rutina; .)
		)
		id														(. var funName = t.val; .)
		'('														(. var vars = new List<Variable>(); .)
		[														(. Type tipo; .)
			Tipo<out tipo> id									(. vars.Add(new Variable(){Name=t.val, Type=tipo}); .) 
			{
				',' Tipo<out tipo> id							(. vars.Add(new Variable(){Name=t.val, Type=tipo}); .) 
			}
		] ')'													(. AddFunction(funName, funType, vars); .)

		(														(. DirectValueSymbol returns; .)
		Bloque<funName, vars.ToArray(), funType != Type.Rutina, out returns>
																(. AddReturns(funName, returns); LinkFunctionBody(funName); .)
		| ';'
		)
		.


	Main =					(. DirectValueSymbol dummy; .) 
		"main" Bloque<"main", new Variable[]{}, false, out dummy>.
	
	Declaracion =					(. Type tipo; bool isArr = false; List<int> sizes = null; .)
		Tipo<out tipo> 
		[ TipoArr<out sizes>		(. isArr = true; .)
		] id						(. AddVariable(t.val, tipo, isArr, sizes); .) 
		{ ',' id					(. AddVariable(t.val, tipo, isArr, sizes); .) 
		} ';'.

	Tipo<out Type tipo> =	(. Type tipoAux = Type.Error; .)
		(
			"entero"		(. tipoAux = Type.Entero; .)
			| "decimal"		(. tipoAux = Type.Decimal; .)
			| "booleano"	(. tipoAux = Type.Booleano; .)
			| "cadena"		(. tipoAux = Type.Cadena; .)
		)					(. tipo = tipoAux; .)	
		.

	TipoArr<.out List<int> lengths.> =						(. lengths = new List<int>(); .)
									'[' cteent				(. lengths.Add(int.Parse(t.val)); .)
									{ 
										',' cteent			(. lengths.Add(int.Parse(t.val)); .) 
									} 
									']'.
	
	Bloque<string name, Variable[] parameters, bool isFunction, out DirectValueSymbol returns> =						
		'{'										(. CreateNewScope(name, new List<Variable>(parameters)); DoPushDefaults(); .)
												(. returns = null; .)
		{
			IF(DoubleFollowedByLPar()) DeclaracionFunc
			| Declaracion 
			| Condicion 
			| Ciclo
			| DrawLine 
			| DrawArc
			| DrawRec
			| DrawEllipse
			| DrawTriangle
			| Impresion 
			| IF(FollowedByLPar())							(. Function function; List<DirectValueSymbol> paras; .) 
				Funcion<out function, out paras> ';'		(. DoRoutine(function, paras); .) 
			| Asignacion
		}														(. CheckFunctionsNoBody(); var hasReturn = false; .)
		[ "regresa" Expresion ';'								(. hasReturn = true; returns = _symbolStack.Pop(); _currentScope.Returns = returns; .)
		] '}'													(. ValidateHasReturn(isFunction, hasReturn); DoPopLocals(); _currentScope = _currentScope.Parent; .)
		.

	Asignacion =					(. Variable variable; .)
		Variable<out variable>		(. _symbolStack.Push(variable); .)
		'=' Expresion ';'			(. DoAssign(); .)
		.

	Funcion<. out Function function, out List<DirectValueSymbol> parameters .> = 
		id				(. string name = t.val; CheckFunctionExists(name); .)
		'('				(. parameters = new List<DirectValueSymbol>(); .)
		[ Expresion		(. parameters.Add(_symbolStack.Pop()); .)
		{',' Expresion	(. parameters.Add(_symbolStack.Pop()); .)
		}] ')'			(. CheckParamAmount(name, parameters.Count); .)
						(. function = _currentScope.Search(name) as Function; .)
		.

	Condicion = 
		"si" '(' Expresion ')'							(. var condition = _symbolStack.Pop(); DirectValueSymbol returnsDummy; .)
		Bloque<"if", new Variable[]{}, false, out returnsDummy>					(. var ifBlock = _currentScope.Children.Last().CommandList; CommandList elseBlock = null; .)
		[
			"sino" Bloque<"else", new Variable[]{}, false, out returnsDummy>		(. elseBlock = _currentScope.Children.Last().CommandList; .)
		]												(. DoIfElse(condition, ifBlock, elseBlock); .)
		.
	
	Ciclo = 
		"mientras" '('							(. CreateNewScope("Expression", new List<Variable>());  DirectValueSymbol returnsDummy; .) 
		Expresion								(. var expression = _currentScope.CommandList; var result = _symbolStack.Pop(); .)
		')'										(. _currentScope = _currentScope.Parent; .)
		 Bloque<"while", new Variable[]{}, false, out returnsDummy>		(. var whileBlock = _currentScope.Children.Last().CommandList; DoWhile(expression, result, whileBlock); .)
		 .
	
	Impresion = "imprimir" '('		(. var expressions = new List<DirectValueSymbol>(); .)
		Expresion					(. expressions.Add(_symbolStack.Pop()); .)
		{',' Expresion				(. expressions.Add(_symbolStack.Pop()); .)
		} ')' ';'					(. DoPrint(expressions); .)
		.
	
	Variable<out Variable variable> = 
		id							(. string name = t.val; CheckVariableExists(name); var symbol = _currentScope.Search(name); variable = symbol as Variable; .) 
		['['						(. var indexes = new List<DirectValueSymbol>(); .)
		 Expresion					(. indexes.Add(_symbolStack.Pop() as DirectValueSymbol); .)
		 { 
			',' Expresion			(. indexes.Add(_symbolStack.Pop() as DirectValueSymbol); .)
		} 
		']'							(. CheckIsArray(name); VariableArray array =(symbol as VariableArray); DoAssignIndex(array,indexes); variable = array; .)
		 ].

	DrawLine = 
		"dibujarLinea" '(' Expresion (. var lineColor = _symbolStack.Pop(); .)
							',' Expresion (. var thickness = _symbolStack.Pop(); .)
							',' Expresion (. var x1 = _symbolStack.Pop(); .)
							',' Expresion (. var y1 = _symbolStack.Pop(); .)
							',' Expresion (. var x2 = _symbolStack.Pop(); .)
							',' Expresion (. var y2 = _symbolStack.Pop(); .)
						')' ';' (. DoLine(lineColor, thickness, x1, y1, x2, y2); .)
	.

	DrawArc =
		"dibujarArco" '(' Expresion (. var lineColor = _symbolStack.Pop(); .)
						',' Expresion (. var thickness = _symbolStack.Pop(); .)
						',' Expresion (. var x = _symbolStack.Pop(); .)
						',' Expresion (. var y = _symbolStack.Pop(); .)
						',' Expresion (. var width = _symbolStack.Pop(); .)
						',' Expresion (. var height = _symbolStack.Pop(); .)
						',' Expresion (. var startAngle = _symbolStack.Pop(); .)
						',' Expresion (. var finalAngle = _symbolStack.Pop(); .)
					')' ';' (. DoArc(lineColor, thickness, x, y, width, height, startAngle, finalAngle); .)
	.

	DrawRec =
		"dibujarRectangulo" '(' Expresion (. var backgroundColor = _symbolStack.Pop(); .)
								',' Expresion (. var lineColor = _symbolStack.Pop(); .)
								',' Expresion (. var thickness = _symbolStack.Pop(); .)
								',' Expresion (. var x = _symbolStack.Pop(); .)
								',' Expresion (. var y = _symbolStack.Pop(); .)
								',' Expresion (. var width = _symbolStack.Pop(); .)
								',' Expresion (. var height = _symbolStack.Pop(); .)
							')' ';' (. DoRectangle(backgroundColor, lineColor, thickness, x, y, width, height); .)
	.

	DrawEllipse =
		"dibujarElipse" '(' Expresion (. var backgroundColor = _symbolStack.Pop(); .)
							',' Expresion (. var lineColor = _symbolStack.Pop(); .)
							',' Expresion (. var thickness = _symbolStack.Pop(); .)
							',' Expresion (. var x = _symbolStack.Pop(); .)
							',' Expresion (. var y = _symbolStack.Pop(); .)
							',' Expresion (. var width = _symbolStack.Pop(); .)
							',' Expresion (. var height = _symbolStack.Pop(); .)
						')' ';' (. DoEllipse(backgroundColor, lineColor, thickness, x, y, width, height); .)
	.

	DrawTriangle =
		"dibujarTriangulo" '(' Expresion (. var backgroundColor = _symbolStack.Pop(); .)
								',' Expresion (. var lineColor = _symbolStack.Pop(); .)
								',' Expresion (. var thickness = _symbolStack.Pop(); .)
								',' Expresion (. var x1 = _symbolStack.Pop(); .)
								',' Expresion (. var y1 = _symbolStack.Pop(); .)
								',' Expresion (. var x2 = _symbolStack.Pop(); .)
								',' Expresion (. var y2 = _symbolStack.Pop(); .)
								',' Expresion (. var x3 = _symbolStack.Pop(); .)
								',' Expresion (. var y3 = _symbolStack.Pop(); .)
							')' ';' (. DoTriangle(backgroundColor, lineColor, thickness, x1, y1, x2, y2, x3, y3); .)
	.

	Expresion =
		Exp
		{
			(
				"&&"	(. _operatorStack.Push(Operator.And); .)
				|"||"	(. _operatorStack.Push(Operator.Or); .)
			) 
			Exp			(. DoPendingLogical(); .)
		}.
	
	Exp = 
		Expt
		[
			(
				'>'		(. _operatorStack.Push(Operator.GreaterThan); .)
				|'<'	(. _operatorStack.Push(Operator.LessThan); .)
				|">="	(. _operatorStack.Push(Operator.GreaterEqual); .)
				|"<="	(. _operatorStack.Push(Operator.LessEqual); .)
				|"<>"	(. _operatorStack.Push(Operator.Different); .)
				|"=="	(. _operatorStack.Push(Operator.Equality); .)
			) 
			Expt		(. DoPendingRelational(); .)
		].
	
	Expt = 
		Termino
		{
			(
				'+'		(. _operatorStack.Push(Operator.Sum); .) 
				| '-'	(. _operatorStack.Push(Operator.Minus); .)
			) 
			Termino		(. DoPendingSum(); .)
		}.
	
	Termino = 
		Factor
		{
			(
				'*'		(. _operatorStack.Push(Operator.Multiply); .) 
				|'/'	(. _operatorStack.Push(Operator.Divide); .)
				|'%'	(. _operatorStack.Push(Operator.Modulo); .)
			) 
			Factor		(. DoPendingMultiplication(); .)
		}.
	
	Factor =				(. var negative = false; .)
		[('+' | '-'			(. negative = true; .)
		)]
		((
			'('				(. _operatorStack.Push(Operator.FakeLimit); .)
			Expresion 
			')'				(. _operatorStack.Pop(); .)
		) 
		| 
		(							(. DirectValueSymbol symbol; .)
			Constante<out symbol>	(. _symbolStack.Push(symbol); .)
		))							(. if(negative) DoNegative(); .)
		.
	
	Constante<out DirectValueSymbol sym> = (. sym = null; .)
		(cteent							(. sym = _constBuilder.IntConstant(t.val); .)
		| ctedbl						(. sym = _constBuilder.DecConstant(t.val); .)
		| Ctebol						(. sym = _constBuilder.BoolConstant(t.val); .)
		| ctestr						(. sym = _constBuilder.StrConstant(t.val); .)
		| Aleatorio						(. sym = _constBuilder.DecConstant("0"); DoRandom(sym); .)
		| Lectura						(. sym = _constBuilder.StrConstant(""); DoRead(sym); .)
		| IF(FollowedByLPar())			(. Function function; List<DirectValueSymbol> parameters;.)
			Funcion<out function, out parameters>		(. var result = _varBuilder.NewVariable(function.Type); DoFunction(function, parameters, result); sym = result; _currentScope.Add(sym); .)
		|								(. Variable variable; .)
			Variable<out variable>		(. sym = variable; .)
		).
	
	Aleatorio = "aleatorio" '(' ')'.
	
	Lectura = "lectura" '(' ')'.
	
	Ctebol = "verdadero" | "falso".

END ESpp.